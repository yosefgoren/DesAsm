%{
	int yylex();
	void yyerror(const char* s);
	#include "Compiler/Compiler.h"
	#include <string>
	#include <vector>
	#include <iostream>
	#include "Compiler/Symtab.h"
	extern Symtab symtab;
	using namespace std;

	extern int yylineno;
	bool parsing_curve;

	#define YYERROR_VERBOSE 1
%}

%union{
//lexer fields:
	std::string* text;
//semantic properties:
	std::vector<std::string>* symbol_list;
	std::vector<std::string>* param_list;
	bool boolean;
};

%token CURVE DISPLAY FUNC FOR IN IMPORT
%token COMMA AND_ALSO
%token LPAREN RPAREN LBRACE RBRACE

%left <text> BINOP
%token <text> NUMBER ID FILENAME
%token <symbol_list> COLON
%type <text> Statment Exp ExpList FuncCall FuncCallTail
%type <symbol_list> Statments
%type <param_list> FormalsSpec FormalsList FormalsListTail

%%

Program: Delcerations;
Delcerations: Delcerations Declaration | Declaration;

DeclarationHead: CURVE {parsing_curve = true;}
	| FUNC {parsing_curve = false;}
	;

Declaration: DeclarationHead ID FormalsSpec COLON {
		if(parsing_curve)
			$3->insert($3->begin(), "t");
		symtab.defineFunction(*$2, *$3);
		$4 = new vector<string>(symtab.getVariables());
	} Statments {
		string decl_rvalue;
		vector<string> params = *$4;
		string params_format = symtab.getVariablesFormat(params);
		int num_statments = $6->size();
		if($6->size() > 1){
			//define an anon function,
			//	for each i (natural number input) given as it's fist paramter, it act's like the i'th statment:
			//lvalue:
			std::string aggregate_sym = symtab.getAnonSymbol();//TODO: need to add k_0 or statment split variable here.
			symtab.defineAnonFunc(aggregate_sym, std::vector<std::string>({"k"}));
			
			params.push_back("k");
			std::string anon_params_format = symtab.getVariablesFormat(params);
			lout += symtab.getSymInfo(aggregate_sym).getDsmExp() + anon_params_format + "=";
			//rvalue:
			int counter = 1;
			lout += "\\left\\{";
			for(std::string& symbol : *$6){
				if(symbol != (*$6)[0])
					lout += ",";
				lout += "k=" + std::to_string(counter++) + ":";//TODO: change k_0 to anon symbol. Otherwise there are issues with nested multiple statments!
				lout += symtab.getSymInfo(symbol).getDsmExp() + params_format;
			}
			lout += "\\right\\}\n";
			//rvalue function that uses the anon function:
			decl_rvalue = symtab.getSymInfo(aggregate_sym).getDsmExp()+
					symtab.getVariablesFormat(params, {
						{"t","L_{split}\\left(t, "+to_string(num_statments)+"\\right)"},
						{"k","L_{index}\\left(t, "+to_string(num_statments)+"\\right)"}
					});//here replace 't' with L_split and 'k_0' with L_indx
			symtab.closeScope();//this closes the scope opened by the anon function.
		} else {
			decl_rvalue = symtab.getSymInfo((*$6)[0]).getDsmExp() + params_format;
		}
		lout += symtab.getSymInfo(*$2).getDsmExp() + params_format + "=" + decl_rvalue + "\n";
		symtab.closeScope();
		delete $2; delete $4; delete $6;
	}
	| DISPLAY {symtab.openScope({"t"});} Exp {lout += *$3 + "\n"; symtab.closeScope(); delete $3;}
	| IMPORT FILENAME {
		// string filename = *$2;
		// if(filename.size() < 3)
		// 	throw runtime_error("expected \"<filename>\" name after 'import' command.");
		// //remove '"' from filename start and ending:
		// filename = filename.substr(1, filename.size()-2);
		// //add "..\"	to filename:
		// filename = "..\\" + filename;
		// //start reading from new file:
		// // cout << "chaning import src to: " << filename << endl;//DB
		// // imported_files.insert(filename);
		// InputManager::getInstance().pushInputFile(filename);
		delete $2;
	};
	;

FormalsSpec: LPAREN FormalsList RPAREN {$$ = $2;}
	| {$$ = new std::vector<std::string>();};
FormalsList: FormalsListTail {$$ = $1;}
	| {$$ = new std::vector<std::string>();};
FormalsListTail: ID COMMA FormalsListTail {$3->insert($3->begin(), *$1); delete $1; $$ = $3;}
	| ID {$$ = new std::vector<std::string>({*$1}); delete $1;};

Statments: Statments AND_ALSO Statment {
		if(!parsing_curve){
			yyerror("consecutive statments are only allowed in 'curve' declarations");
		}
		$1->push_back(*$3);
		$$ = $1;
		delete $3;
	}
	| Statment {$$ = new std::vector<std::string>({*$1}); delete $1;}
	;

Statment: Exp {
		$$ = new string(symtab.getAnonSymbol());
		lout += symtab.defineAnonFunc(*$$) + symtab.getLocalsInFormat() + " = " + *$1 + "\n";
		symtab.closeScope();
	}
	| FOR ID IN Exp COLON {
		if(!parsing_curve){
			yyerror("loops are only allowed in 'curve' declarations");
		}
		$5 = new vector<string>({symtab.getAnonSymbol()});
		//open scope and inside - declare ID:
		symtab.defineAnonFunc((*$5)[0], std::vector<std::string>({*$2}));
	}
	Statment {
		$$ =  new string((*$5)[0]);
		//output anon func definition in format 'func(x, y, ...)', 'ID' is not a parameter.
		lout += symtab.getSymInfo(*$$).getDsmExp()+symtab.getLocalsInFormat({}, {*$2})+" = ";
		
		//output value of our anon func: a call to the 'Statment' anon func,
		//	it's 't' parameter is replaced with 'L_split' and it's 'ID' parameter is replaced with 'L_index':
		lout += symtab.getSymInfo(*$7).getDsmExp()+symtab.getLocalsInFormat({
			{"t","L_{split}\\left(t, "+*$4+"\\right)"},
			{*$2,"L_{index}\\left(t, "+*$4+"\\right)"},
		});
		lout += "\n";
		symtab.closeScope();
		delete $2; delete $4; delete $5; delete $7;
	}
	;

Exp: NUMBER {$$ = $1;}
	| LPAREN Exp COMMA Exp RPAREN {$$ = new string("\\left("+*$2+" , "+*$4+"\\right)"); delete $2; delete $4;}
	| LBRACE Exp COMMA Exp COMMA Exp RBRACE
		{$$ = new string("\\left["+*$2+" , "+*$4+" , "+*$6+"\\right]"); delete $2; delete $4; delete $6;}
	| LPAREN Exp RPAREN {$$ = $2; *$$ = "\\left("+*$$+"\\right)";}
	| ID {$$ = $1; *$$ = symtab.getSymInfo(*$$).getDsmExp();}
	| Exp BINOP Exp {$$ = new string(*$1+" "+*$2+" "+*$3); delete $1; delete $2; delete $3;}
	| FuncCall {$$ = $1;}
	| ID LBRACE NUMBER RBRACE {$$ = $1; *$$ = symtab.getSymInfo(*$$).getDsmExp()+"\\left["+*$3+"\\right]"; delete $3;}
	;

	FuncCall: ID FuncCallTail {
		$$ = new string(symtab.getSymInfo(*$1).getDsmExp()+*$2);
		delete $1;
		delete $2;
	};
FuncCallTail: LPAREN ExpList RPAREN {$$ = $2; *$$ = "\\left("+*$$+"\\right)";};

ExpList: Exp COMMA ExpList {$$ = $1; *$$ += " , "+*$3; delete $3;}
	| Exp {$$ = $1;}
	| {$$ = new string(" ");};
%%

